plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

static def releaseDate() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("GMT+08:00"))
}

android {
    compileSdk 31
    defaultConfig {
        applicationId "com.bearya.mobile.car"
        minSdk 21
        targetSdk 31
        buildFeatures {
            viewBinding true
        }
    }

    signingConfigs {
        config {
            keyAlias 'BeiYa'
            storePassword 'BeiYa123'
            storeFile file('../bearya_keystore.jks')
            keyPassword 'BeiYa!@#'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false
            buildConfigField "String", "ApiUrl", BASE_URL
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            buildConfigField "String", "ApiUrl", BASE_URL
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java']
            }
            res {
                srcDirs = ['src/main/res']
            }
            assets {
                srcDirs = ['src/main/assets']
            }
        }

        world {
            java {
                srcDirs = ['src/world/java']
            }
            res {
                srcDirs = ['src/world/res']
            }
        }

        robot {
            java {
                srcDirs = ['src/robot/java']
            }
            res {
                srcDirs = ['src/robot/res']
            }
        }
    }

    productFlavors {

        world {
            versionCode 234
            versionName "2.3.4"
            buildConfigField "int", "APP_TYPE", "13"
            manifestPlaceholders = [appName: "贝芽创世界"]
            dimension "default"
        }

        robot {
            versionCode 300
            versionName "3.0.0"
            applicationIdSuffix ".robot"
            buildConfigField "int", "APP_TYPE", "14"
            manifestPlaceholders = [appName: "贝芽机器人"]
            dimension "default"
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if ("release" == buildType.name)
                output.outputFileName =
                        "bearya_car_${variant.productFlavors[0].name}_v${variant.productFlavors[0].versionName}_${releaseDate()}.apk"
            else
                output.outputFileName =
                        "${variant.productFlavors[0].name}_v${variant.productFlavors[0].versionName}.apk"
        }
    }

}

dependencies {

    implementation libs.bundles.kotlin
    implementation libs.bundles.retrofit
    implementation libs.bundles.lifecycle

    implementation 'androidx.startup:startup-runtime:1.1.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.vmadalin:easypermissions-ktx:1.0.0'

    implementation 'com.airbnb.android:lottie:4.2.2'
    implementation 'com.vise.xiaoyaoyou:baseble:2.0.6'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.7'
    implementation 'com.kaopiz:kprogresshud:1.2.0'
    implementation 'io.github.razerdp:BasePopup:3.2.0'
    implementation 'com.github.kelinZhou:ApkUpdater:3.1.0'
}